plugins {
	id "com.diffplug.spotless" version "6.15.0"
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id 'com.palantir.git-version' version '0.15.0'
	id 'idea'
	id 'java-library'
}

description = "kafka-connect-s3"

allprojects  {
  apply plugin: 'idea'
  apply plugin: 'maven-publish'

  group = 'com.spredfast.kafka.connect.s3'
  version = '1.0.8'

  apply plugin: 'java-library'
  sourceCompatibility = 11
  targetCompatibility = 11

	apply plugin: "com.diffplug.spotless"
	spotless {
		java {
			removeUnusedImports()
			cleanthat()
			googleJavaFormat()
			formatAnnotations()
		}
	}

  repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
  }
}

apply plugin: "com.github.johnrengelman.shadow"
shadowJar {
	dependencies {
		// provided in the connect classpath
		exclude(dependency('org.apache.kafka:connect-api:2.8.0'))
		exclude(dependency('org.apache.kafka:kafka-clients:2.8.0'))
		exclude(dependency('net.jpountz.lz4:.*:.*'))
		exclude(dependency('org.xerial.snappy:.*:.*'))
		exclude(dependency('org.slf4j:.*:.*'))
	}

	// for things we directly depend on, repackage so we don't conflict with other connectors
	relocate 'com.amazonaws', 'com.spredfast.shade.amazonaws'
	relocate 'com.fasterxml', 'com.spredfast.shade.fasterxml'
	relocate 'org.apache.commons', 'com.spredfast.shade.apache.commons'
	relocate 'org.apache.http', 'com.spredfast.shade.apache.http'
	relocate 'org.joda', 'com.spredfast.shade.joda'
}

dependencies {
	implementation project(':sink')
	implementation project(':source')
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier.set('javadoc')
	from subprojects*.tasks.javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
	from subprojects*.sourceSets.main.allSource
	archiveClassifier.set('sources')
}

jar {
	from subprojects*.sourceSets.main.output
}

artifacts {
	archives javadocJar, sourcesJar
}

ext {
	pomHumanName = 'Kafka Connect S3'
	pomDesc = 'S3 Source and Sink Connectors for Kafka Connect'
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.spredfast.kafka.connect.s3'
			artifactId = 'kafka-connect-s3'

			from components.java

			pom {
				name = 'Kafka Connect S3'
				description = 'S3 Source and Sink Connectors for Kafka Connect'
			}
		}
	}

	repositories {
		maven {
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}

			url = "https://nexus.k8s-use1.ea.prod.sugar.build/repository/cxp-public/"
		}
	}
}
