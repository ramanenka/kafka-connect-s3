description = "Kafka Connect Sink that writes to S3"

apply plugin: "com.github.johnrengelman.shadow"

shadowJar {
	dependencies {
		// provided in the connect classpath
		exclude(dependency('org.apache.kafka:connect-api'))
		exclude(dependency('org.apache.kafka:kafka-clients'))
		exclude(dependency('net.jpountz.lz4:.*:.*'))
		exclude(dependency('org.xerial.snappy:.*:.*'))
		exclude(dependency('org.slf4j:.*:.*'))
	}

	// for things we directly depend on, repackage so we don't conflict with other connectors
	relocate 'com.amazonaws', 'com.spredfast.shade.amazonaws'
	relocate 'com.fasterxml', 'com.spredfast.shade.fasterxml'
	relocate 'org.apache.commons', 'com.spredfast.shade.apache.commons'
	relocate 'org.apache.http', 'com.spredfast.shade.apache.http'
	relocate 'org.joda', 'com.spredfast.shade.joda'
}

configurations {
	extLibs
}

dependencies {
    implementation project(':common')
	testImplementation("junit:junit")
	testImplementation("org.awaitility:awaitility:4.2.0")
	testImplementation("org.mockito:mockito-core:4.9.0")
	testImplementation("io.debezium:debezium-testing-testcontainers")
	testImplementation("net.mguenther.kafka:kafka-junit")
	testImplementation("org.testcontainers:kafka")
	testImplementation("org.testcontainers:localstack")
	testImplementation("ch.qos.logback:logback-core:1.4.5")
	testImplementation("ch.qos.logback:logback-classic:1.4.5")

	extLibs project(path: ':source', configuration: 'shadow' )
}

tasks.register('copyLibs', Copy) {
	from configurations.extLibs {
		into 'build/libs'
	}
}

tasks.build.dependsOn tasks.shadowJar

test {
	testLogging {
		outputs.upToDateWhen {false}
		showStandardStreams = true
	}
}

tasks.test.dependsOn tasks.shadowJar, tasks.copyLibs

